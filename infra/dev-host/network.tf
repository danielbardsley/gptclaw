resource "aws_vpc" "dev" {
  cidr_block           = var.vpc_cidr
  enable_dns_support   = true
  enable_dns_hostnames = true

  tags = {
    Name = "${local.name_prefix}-vpc"
  }
}

resource "aws_internet_gateway" "dev" {
  vpc_id = aws_vpc.dev.id

  tags = {
    Name = "${local.name_prefix}-igw"
  }
}

resource "aws_subnet" "dev_public" {
  vpc_id                  = aws_vpc.dev.id
  cidr_block              = var.subnet_cidr
  availability_zone       = var.availability_zone
  map_public_ip_on_launch = true

  tags = {
    Name = "${local.name_prefix}-public"
  }
}

resource "aws_route_table" "dev_public" {
  vpc_id = aws_vpc.dev.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.dev.id
  }

  tags = {
    Name = "${local.name_prefix}-public"
  }
}

resource "aws_route_table_association" "dev_public" {
  subnet_id      = aws_subnet.dev_public.id
  route_table_id = aws_route_table.dev_public.id
}

resource "aws_security_group" "dev_host" {
  name        = "${local.name_prefix}-host"
  description = "GptClaw development host: no inbound traffic"
  vpc_id      = aws_vpc.dev.id

  tags = {
    Name = "${local.name_prefix}-host"
  }
}

resource "aws_vpc_security_group_egress_rule" "dev_host_ipv4" {
  security_group_id = aws_security_group.dev_host.id
  description       = "Bootstrap and development service egress"
  cidr_ipv4         = "0.0.0.0/0"
  ip_protocol       = "-1"
}
